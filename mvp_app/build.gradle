//构建为lib,plugin在AS里取值一般为'com.android.library'或者'com.android.application'
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
//apply plugin: 'com.neenbedankt.android-apt'

//定义一个时间格式化函数
def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    //编译需要SDK版本
    compileSdkVersion 23
    //SDK Manager确定本地安装该版本才可以
    buildToolsVersion "23.0.2"
    //配置项目默认参数
    defaultConfig {
        applicationId "com.example.relicemxd.leanrrv"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        //友盟多渠道打包
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "happysong"]
    }
    //渠道名
    productFlavors {
        relice {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "relice"]
        }
        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
    }

    //编译项 - gradle中的BuildType对象
    buildTypes {
        //配置release正式包参数,也可配置debug{ }参数
        release {
            //注明是否需要混淆
            minifyEnabled false
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //进行多渠道打包,并命名
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为xiuandroid_v1.0_20150115_wandoujia.apk(默认获取第一个渠道名)
                        def fileName = "HappySong_Android_${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}_.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

def Dagger2_Version = '2.0'
//依赖支持对象
dependencies {
    //使用自定义gragle(具体查看dependencies.gradle)
    Map<String, String> dependencies = rootProject.ext.dependencies
    compile dependencies.retrofit
    compile dependencies.retrofitConverterGson
    compile dependencies.retrofitAdapterRxJava

    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    //编译第三方依赖库
    compile project(':mylibrary')

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:x.y.z'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.github.Syehunter:RecyclerViewManager:0.1.6.4'

    // Dagger 2.0
//    compile "com.google.dagger:dagger:$Dagger2_Version"
//    apt "com.google.dagger:dagger-compiler:$Dagger2_Version"
//    compile 'org.glassfish:javax.annotation:10.0-b28'
}
